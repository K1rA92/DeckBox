buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'io.pokemontcg:pokemon-tcg-sdk-kotlin:1.0.13'
    }
}

import io.pokemontcg.requests.CardQueryBuilder
import io.pokemontcg.Pokemon
import io.pokemontcg.model.Card
import kotlin.Unit
import kotlin.jvm.functions.Function1

/**
 * Custom task to output a random list of pokemon card images for use in [app/src/main/java/com/r0adkll/deckbuilder/util/CardUtils.kt]
 * that powers the scrolling marquee of cards on the login/setup screen for the user.
 */
task pokePrint {
    doLast {
        def poke = new Pokemon()
        def cards = poke.card().where(new Function1<CardQueryBuilder, Unit>() {
            @Override
            Unit invoke(CardQueryBuilder cardQueryBuilder) {
                cardQueryBuilder.pageSize = 1000
                cardQueryBuilder.setCode = "sm2|sm3|sm35|sm4|sm5"
                return null
            }
        }).all()

        Collections.shuffle(cards)
        for (Card card : cards) {
            println "\"${card.imageUrl}\","
        }
    }

}