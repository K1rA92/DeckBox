apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.oss.licenses.plugin'
apply plugin: 'io.fabric'

def GIT_SHA = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def GIT_TAG =  'git describe --tags'.execute([], project.rootDir).text.trim()

def VERSION_NAME = "1.7.5"


ext.gitVersioner = [
        defaultBranch           : "master",
        stableBranches          : ["release"],
        yearFactor              : 1200,
        snapshotEnabled         : false,
        localChangesCountEnabled: false,
        shortName: { gitVersion ->
            return gitVersion.commit.subSequence(0, 7)
        }
]
apply from: 'https://raw.githubusercontent.com/passsy/gradle-GitVersioner/master/git-versioner.gradle'


android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.r0adkll.deckbuilder"
        buildToolsVersion rootProject.ext.buildToolsVersion

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode gitVersion.version
        versionName "$VERSION_NAME.${gitVersion.version}"
        multiDexEnabled true

        buildConfigField "String", "GIT_SHA", "\"$GIT_SHA\""
        buildConfigField "String", "GIT_TAG", "\"$GIT_TAG\""
        buildConfigField "String", "DATABASE_NAME", "\"cards.db\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == "release") {
                outputFileName = "deckbox-" + variant.versionName + "." + GIT_SHA.toUpperCase() + ".apk"
            }
        }
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

dependencies {
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "org.amshove.kluent:kluent:$kluent_version"
    androidTestImplementation "org.amshove.kluent:kluent:$kluent_version"
    androidTestImplementation "com.android.support.test:runner:$runner_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espresso_version"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "io.pokemontcg:pokemon-tcg-sdk-kotlin:$poketcg_version"

    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:preference-v7:$support_version"
    implementation "com.android.support:preference-v14:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:palette-v7:$support_version"


    implementation "com.google.firebase:firebase-core:16.0.3"
    implementation "com.google.firebase:firebase-firestore:17.1.0"
    implementation "com.google.firebase:firebase-auth:16.0.3"
    implementation "com.google.firebase:firebase-config:16.0.0"
    implementation "com.google.android.gms:play-services-auth:16.0.0"
    implementation "com.google.android.gms:play-services-oss-licenses:16.0.0"
    releaseImplementation 'com.crashlytics.sdk.android:crashlytics:2.9.5'

    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    implementation 'com.caverock:androidsvg-aar:1.3'

    implementation "nz.bradcampbell:paperparcel:$paperparcel_version"
    implementation "nz.bradcampbell:paperparcel-kotlin:$paperparcel_version"
    kapt "nz.bradcampbell:paperparcel-compiler:$paperparcel_version"

    implementation "com.evernote:android-state:$state_version"
    kapt "com.evernote:android-state-processor:$state_version"

    implementation "io.requery:requery:$requery_version"
    implementation "io.requery:requery-android:$requery_version"
    implementation "io.requery:requery-kotlin:$requery_version"
    kapt "io.requery:requery-processor:$requery_version"

    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rxbinding_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:$rxbinding_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$rxbinding_version"
    implementation "com.jakewharton.rxrelay2:rxrelay:$rxrelay_version"
    implementation "com.f2prateek.rx.preferences2:rx-preferences:$rxpref_version"

    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    implementation("com.52inc:52Kit-core:$ftkit_version") {
        exclude group: 'io.reactivex'
    }
    implementation("com.52inc:52Kit-winds:$ftkit_version") {
        exclude group: 'io.reactivex'
    }
    implementation("com.52inc:52Kit-kotlin:$ftkit_version") {
        exclude group: 'io.reactivex'
    }
    implementation("com.52inc:52Kit-arch:$ftkit_version") {
        exclude group: 'io.reactivex'
    }

    implementation "com.jakewharton.timber:timber:$timber_version"
    implementation "com.roughike:bottom-bar:$bottombar_version"
    implementation 'com.nex3z:flow-layout:1.2.2'
    implementation 'com.sothree.slidinguppanel:library:3.4.0'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    debugImplementation "com.facebook.stetho:stetho:$stetho_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'nl.littlerobots.rxlint:rxlint:1.6.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

}

apply plugin: 'com.google.gms.google-services'
apply from: '../tools/pokken.gradle'
